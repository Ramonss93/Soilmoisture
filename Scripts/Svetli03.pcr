# step 1: local drainage directions
tldd=lddcreate(tarrawar.map,1e35,1e35,1e35,1e35);

# step 2: contributing area
carea=accuflux(tldd, 25.0);
lcarea=ln(carea);

# make catchment mask
outlet=boolean(if(carea>12e3 then 1 else 0));
catchment=catchment(tldd,outlet);
catchm_inv= boolean(if(catchment then 0 else 1));

# construction of hydro. network - and hillslopes
tval=8000.0;
chnet=boolean(if(carea>tval then 1 else 0));
hslopes=boolean(if(carea>tval then 0 else 1));

# aspect map
aspect=aspect(tarrawar.map);

# inversion of dem for upslope length
dem_inv = 500 - tarrawar.map;
tldd_inv = lddcreate(dem_inv,1e35,1e35,1e35,1e35);
lup_inv = ldddist(tldd_inv, catchm_inv, 1);

# relative length of slopes
ldown = ldddist(tldd, chnet, 1);
ldown = if(chnet then 0 else ldown);
lup=slopelength(tldd,1);
#lup = if(chnet then )
lmax=ldown+lup_inv;
lrel=lup_inv/lmax;
lrel=if(chnet then 1 else lrel);
lrel=windowaverage(lrel, 15.0);

# profile curvature
kp = profcurv(tarrawar.map);
kp=windowaverage(kp, 15.0);

# use it to remove erroneous curvature values
#kp=scalar(if(catchment==1 then kp else 0.0));

# alternative: constrain -0.02 < kp < 0.02
tval=0.005;
kp=max(kp,-tval);
kp=min(kp, tval);

# find convex, concave cells
convex=boolean(if(kp>0 then 1 else 0));
concave=boolean(if(kp<=0 then 1 else 0));

# slope etc
alpha=scalar(atan(slope(tarrawar.map)));
alpha0=7.5;
ke=0.0;
coordsysangl = 14; 
A=scalar(aspect);
north=A<(45+coordsysangl) or A>=(315+coordsysangl);
east=A>=(45+coordsysangl) and A<(135+coordsysangl);
south=A>=(135+coordsysangl) and A<(225+coordsysangl);
west=A>=(225+coordsysangl) and A<(315+coordsysangl);
ke=if(north then  0.005 else ke);
ke=if(east  then  0.002 else ke);
ke=if(south then -0.01  else ke);
ke=if(west  then -0.003 else ke);

Ka=(1-ke*alpha)/(1-ke*alpha0);

# wetness: straight/concave
c1=lrel<=0.167;
c2=lrel>0.167 and lrel<=0.833;
c3=lrel>0.833;
af1=1-0.2*lrel**0.5;
af2=0.77+0.43*lrel**1.47;
af3=0.77+0.70*lrel**4.0;
a=if(c1 then af1 else if(c2 then af2 else af3));
Kwa=(a+0.16*cos(A+180)+0.09*sin(A))*Ka;

# wettness: convex
c4=convex and lrel<=0.833;
c5=convex and lrel>0.8333;
bf4=1.04-0.22*lrel**0.93;
bf5=0.86+18.0*(lrel-0.833)**2.0;
b=if(c4 then bf4 else if (c5 then bf5 else 0.0));
Kwb=(b+0.14*cos(A+180.0)+0.10*sin(A)-0.02*cos(2*A))*Ka;

# wetness: combine
Kw=0.0;
Kw=if(concave then Kwa else Kw);
Kw=if(convex then Kwb else Kw);
Kw=windowaverage(Kw, 10.0);
Kw=if(catchment then Kw else 0);

cmap=if(c1 then 1 else if(c2 then 2 else if(c3 then 3 else if(c4 then 4 else if(c5 then 5 else 0)))));
report cmap.map=scalar(cmap);

#report output.map=a;
#report aspect.map=scalar(aspect)*(180.0/3.141592645);
#report aspect.map=scalar(aspect);
#report ke.map=ke;
#report lrel.map=lrel;
#report lup.map=lup;
#report lup_inv.map = lup_inv;
#report ldown.map=ldown;
#report lmax.map=lmax;
report kw.map=Kw;
#report ka.map=Ka;
#report alpha.map=alpha;
report boundary.map=catchment;
#report aspect.map=A;
report chnet.map = chnet;
#report curv.map = kp;
#report draindir = tldd;